///$tab Настройки
SET ThousandSep=' ';
SET DecimalSep=',';
SET MoneyThousandSep=' ';
SET MoneyDecimalSep=',';
SET MoneyFormat='# ##0,00р.;-# ##0,00р.';
SET TimeFormat='h:mm:ss';
SET DateFormat='DD.MM.YYYY';
SET TimestampFormat='DD.MM.YYYY h:mm:ss[.fff]';
SET MonthNames='янв;фев;мар;апр;май;июн;июл;авг;сен;окт;ноя;дек';
SET DayNames='Пн;Вт;Ср;Чт;Пт;Сб;Вс';
SET LongMonthNames='Январь;Февраль;Март;Апрель;Май;Июнь;Июль;Август;Сентябрь;Октябрь;Ноябрь;Декабрь';
SET LongDayNames='понедельник;вторник;среда;четверг;пятница;суббота;воскресенье';
SET FirstWeekDay=0;
SET BrokenWeeks=1;
SET ReferenceDay=0;
SET FirstMonthOfYear=1;
SET CollationLocale='ru-RU';

///$tab Факты
 //////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//
// Процедура загрузки данных о фактах
//
SUB Load_Facts
  // загружаем данные об остатках.
facts:
  LOAD 
     [Склад]
    ,date([ГодМесяц])               AS [ОтчетнаяДата]
    ,[Остатки на конец месяца, шт]  AS [Остаток]
    FROM
      [raw_source\Данные по остаткам.xlsx] (OOXML, EMBEDDED LABELS, TABLE IS Sheet1)
  ;

  // загружаем свежие данные о продажах агрегируя их помесячно и сливаем с остатками
 OUTER JOIN (facts) LOAD 
     [Склад]
    ,date(monthStart([Дата])) AS [ОтчетнаяДата]
    ,sum([Продажи рублей])    AS [Продажи]
    FROM
      [raw_source\Данные по продажам.xls] (BIFF, EMBEDDED LABELS, TABLE IS Sheet1$)
    GROUP BY
       [Склад] 
      ,monthStart([Дата])
  ;
ENDSUB

///$tab Календарь
 //////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//
// Процедура формирования календаря на основе фактов.
//
SUB Calendar_Builder

  // аналог (с нюансами -- собирает уникальные значения по полю, а не по таблице) 
  // LOAD DISTINCT [ОтчетнаяДата] RESIDENT facts, но существенно быстрее.
facts_dates_list:
  LOAD fieldValue('ОтчетнаяДата', recNo()) AS [ОтчетнаяДата] AUTOGENERATE FieldValueCount('ОтчетнаяДата');

calendar:
  LOAD 
     [ОтчетнаяДата]
    ,date([ОтчетнаяДата], 'YYYY.MM')  AS [ГодМесяц]
    ,dual('thisYear', 0)              AS [Признак]
    RESIDENT 
      facts_dates_list
  ;


  CONCATENATE (calendar) LOAD
     [ГодМесяц]
    ,addYears([ОтчетнаяДата], -iterNo())  AS [ОтчетнаяДата]
    ,dual('year-' & iterNo(), -iterNo())  AS [Признак]
    RESIDENT
      calendar
    WHILE
      iterNo() <= 2
  ;

  // откидываем из календаря строки, по которым нет фактов.
  RIGHT JOIN (calendar) LOAD DISTINCT [ОтчетнаяДата] RESIDENT facts_dates_list;
  DROP TABLE facts_dates_list;

  JOIN (calendar) LOAD
     [ГодМесяц]
    ,year([ГодМесяц])  AS [Год]
    ,month([ГодМесяц]) AS [Месяц]
    RESIDENT
      calendar
  ;
ENDSUB;

///$tab Главная
 //////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//
// Главная
//
SUB Main
  CALL Load_Facts;
  CALL Calendar_Builder;

min_max_date:
  LOAD min([ОтчетнаяДата]) AS min_rep_date, max([ОтчетнаяДата]) AS max_rep_date;
    LOAD fieldValue('ОтчетнаяДата', recNo()) AS [ОтчетнаяДата] AUTOGENERATE FieldValueCount('ОтчетнаяДата')
  ;

  LET v_min_rep_date = date(peek('min_rep_date', 0, 'min_max_date'));
  LET v_max_rep_date = date(peek('max_rep_date', 0, 'min_max_date'));
  DROP TABLE min_max_date;
ENDSUB

CALL Main;